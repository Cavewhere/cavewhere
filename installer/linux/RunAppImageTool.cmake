foreach(_var IN ITEMS VERSION_FILE SOURCE_DIR APPIMAGE_APPDIR LINUXDEPLOY_EXECUTABLE LINUXDEPLOY_ENV_PATH APPIMAGE_RUNTIME_FILE APPIMAGE_SYSTEM_PROCESSOR)
    if(DEFINED ${_var})
        string(REPLACE "\"" "" ${_var} "${${_var}}")
    endif()
endforeach()
if(NOT DEFINED VERSION_FILE)
    message(FATAL_ERROR "VERSION_FILE not set")
endif()
if(NOT EXISTS "${VERSION_FILE}")
    if(NOT DEFINED SOURCE_DIR)
        message(FATAL_ERROR "Version file not found: ${VERSION_FILE}")
    endif()
    execute_process(
        COMMAND git -C "${SOURCE_DIR}" describe HEAD
        OUTPUT_VARIABLE CAVEWHERE_VERSION
        OUTPUT_STRIP_TRAILING_WHITESPACE
        RESULT_VARIABLE GIT_DESCRIBE_RESULT
    )
    if(NOT GIT_DESCRIBE_RESULT EQUAL 0 OR CAVEWHERE_VERSION STREQUAL "")
        message(FATAL_ERROR "Version file not found: ${VERSION_FILE}")
    endif()
    file(WRITE "${VERSION_FILE}" "${CAVEWHERE_VERSION}\n")
endif()
file(READ "${VERSION_FILE}" CAVEWHERE_VERSION)
string(STRIP "${CAVEWHERE_VERSION}" CAVEWHERE_VERSION)
if(CAVEWHERE_VERSION STREQUAL "")
    message(FATAL_ERROR "Version file is empty: ${VERSION_FILE}")
endif()

if(NOT DEFINED APPIMAGE_APPDIR)
    message(FATAL_ERROR "APPIMAGE_APPDIR not set")
endif()
if(NOT DEFINED LINUXDEPLOY_EXECUTABLE)
    message(FATAL_ERROR "LINUXDEPLOY_EXECUTABLE not set")
endif()
if(NOT DEFINED LINUXDEPLOY_ENV_PATH)
    message(FATAL_ERROR "LINUXDEPLOY_ENV_PATH not set")
endif()
if(NOT DEFINED APPIMAGE_SYSTEM_PROCESSOR)
    message(FATAL_ERROR "APPIMAGE_SYSTEM_PROCESSOR not set")
endif()

set(ENV{PATH} "${LINUXDEPLOY_ENV_PATH}")
set(ENV{LINUXDEPLOY_OUTPUT_VERSION} "${CAVEWHERE_VERSION}")
set(ENV{LDAI_OUTPUT} "CaveWhere-${CAVEWHERE_VERSION}-${APPIMAGE_SYSTEM_PROCESSOR}.AppImage")
if(DEFINED APPIMAGE_RUNTIME_FILE AND NOT APPIMAGE_RUNTIME_FILE STREQUAL "")
    set(ENV{LDAI_RUNTIME_FILE} "${APPIMAGE_RUNTIME_FILE}")
endif()

execute_process(
    COMMAND "${LINUXDEPLOY_EXECUTABLE}" --appdir "${APPIMAGE_APPDIR}" --output appimage
    RESULT_VARIABLE LINUXDEPLOY_RESULT
)
if(NOT LINUXDEPLOY_RESULT EQUAL 0)
    message(FATAL_ERROR "linuxdeploy appimage output failed with code ${LINUXDEPLOY_RESULT}")
endif()
