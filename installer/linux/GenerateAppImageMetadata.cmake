foreach(_var IN ITEMS VERSION_FILE SOURCE_DIR APPIMAGE_DESKTOP_IN APPIMAGE_DESKTOP_FILE APPIMAGE_APPRUN_IN APPIMAGE_APPRUN_FILE)
    if(DEFINED ${_var})
        string(REPLACE "\"" "" ${_var} "${${_var}}")
    endif()
endforeach()
if(NOT DEFINED VERSION_FILE)
    message(FATAL_ERROR "VERSION_FILE not set")
endif()
if(NOT EXISTS "${VERSION_FILE}")
    if(NOT DEFINED SOURCE_DIR)
        message(FATAL_ERROR "Version file not found: ${VERSION_FILE}")
    endif()
    execute_process(
        COMMAND git -C "${SOURCE_DIR}" describe HEAD
        OUTPUT_VARIABLE CAVEWHERE_VERSION
        OUTPUT_STRIP_TRAILING_WHITESPACE
        RESULT_VARIABLE GIT_DESCRIBE_RESULT
    )
    if(NOT GIT_DESCRIBE_RESULT EQUAL 0 OR CAVEWHERE_VERSION STREQUAL "")
        message(FATAL_ERROR "Version file not found: ${VERSION_FILE}")
    endif()
    file(WRITE "${VERSION_FILE}" "${CAVEWHERE_VERSION}\n")
endif()
file(READ "${VERSION_FILE}" CAVEWHERE_VERSION)
string(STRIP "${CAVEWHERE_VERSION}" CAVEWHERE_VERSION)
if(CAVEWHERE_VERSION STREQUAL "")
    message(FATAL_ERROR "Version file is empty: ${VERSION_FILE}")
endif()

if(NOT DEFINED APPIMAGE_DESKTOP_IN OR NOT DEFINED APPIMAGE_DESKTOP_FILE)
    message(FATAL_ERROR "APPIMAGE_DESKTOP_IN/APPIMAGE_DESKTOP_FILE not set")
endif()
if(NOT DEFINED APPIMAGE_APPRUN_IN OR NOT DEFINED APPIMAGE_APPRUN_FILE)
    message(FATAL_ERROR "APPIMAGE_APPRUN_IN/APPIMAGE_APPRUN_FILE not set")
endif()

configure_file("${APPIMAGE_DESKTOP_IN}" "${APPIMAGE_DESKTOP_FILE}" @ONLY)
configure_file("${APPIMAGE_APPRUN_IN}" "${APPIMAGE_APPRUN_FILE}" @ONLY)
